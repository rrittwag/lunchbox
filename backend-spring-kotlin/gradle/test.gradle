// -------------------
//   Unit-Tests mit JUnit 5
// -------------------
dependencies {
  // JUnit 5
  testRuntimeOnly    'org.junit.platform:junit-platform-launcher'
  testImplementation 'org.junit.jupiter:junit-jupiter'

  // webserver
  testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'

  // Kotlin
  testImplementation 'org.amshove.kluent:kluent:1.73'    // assertions
  testImplementation 'com.ninja-squad:springmockk:5.0.1' // support @MockkBean injection

  // external
  testImplementation 'org.springframework.boot:spring-boot-starter-restclient-test'
  testImplementation 'com.squareup.okhttp3:mockwebserver:5.3.2'

  // TestContainers for integration tests
  testIntegrationImplementation platform("org.testcontainers:testcontainers-bom:2.0.3")
  testIntegrationImplementation "org.testcontainers:testcontainers-junit-jupiter"
  testIntegrationImplementation "org.testcontainers:testcontainers-nginx"
}

configurations {
  all*.exclude module: 'mockito-core'          // Mockito is replaced by MockK
}

test {
  useJUnitPlatform()
  testLogging { exceptionFormat = 'full' }
}


// -------------------
//   Integrations-Tests mit JUnit 5
// -------------------
tasks.register('testIntegration', Test) {
  description = 'Führt die Integrations-Tests aus.'
  group = 'Verification'
  shouldRunAfter test

  testClassesDirs = sourceSets.testIntegration.output.classesDirs
  classpath = sourceSets.testIntegration.runtimeClasspath

  useJUnitPlatform()
  maxParallelForks = (int) (Runtime.runtime.availableProcessors().intdiv(2) ?: 1) // Test-Klassen parallel ausführen
  testLogging { exceptionFormat = 'full' }

  reports.html.required = false // Report-Generierung frisst unnötig CI-Zeit
}

tasks.register('testAll') {
  dependsOn tasks.withType(Test)
}


// -------------------
//   Testabdeckung mit Java Code Coverage
// -------------------
apply plugin: "jacoco"

jacocoTestReport {
  executionData tasks.withType(Test) // Ergebnisse der Unit- und Integrations-Tests zur Testabdeckung heranziehen
  doFirst {
    executionData.from = files(executionData.findAll { it.exists() })
  }
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        minimum = 0.5
      }
    } // mindestens 50% Testabdeckung, sonst schlägt Task fehl!
  }
}

// Der Task-Name 'testCoverage' ist eingängiger als 'jacocoTestReport' ...
tasks.register('testCoverage') {
  dependsOn 'jacocoTestReport'
  description = 'Gibt Coverageinformationen aus (Alias für jacocoTestReport).'
  group = 'Verification'
}
